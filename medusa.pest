program = _{ SOI ~ statement_block ~ EOI }

statement_block = _{ newline_or_comment* ~ (statement ~ newline_or_comment*)* }
newline_or_comment = _{ NEWLINE | comment }
comment = _{ ("//" ~ (!(NEWLINE) ~ ANY)* ~ (NEWLINE | EOI)) | ("/*" ~ ANY* ~ "*/") }

statement = _{ (declaration | assignment | input | output | if_ | forloop) ~ ";" }

declaration = { dataType ~ (input | assignment | identifier) }
assignment = {identifier ~ "=" ~ expression}
output = {(expression ~ "->" ~ "@")}
input = {(identifier ~ "<-" ~ "@")}
if_ = { "if" ~ "(" ~ condition ~ ")" ~ "{" ~ statement_block ~ "}" ~ else_? }
else_ = { "else" ~ "{" ~ statement_block ~ "}" }
forloop = { "for" ~ "(" ~ (declaration | assignment | identifier) ~ ";" ~ condition ~ ";" ~ assignment ~ ")" ~ "{" ~ statement_block ~ "}" }

condition = {expression ~ conditional_operator ~ expression}

// https://docs.rs/pest/latest/pest/pratt_parser/struct.PrattParser.html
expression = { prefix* ~ primary ~ (infix ~ prefix* ~ primary)* }

prefix  = _{ cast }
primary = _{ float | int | string | identifier | "(" ~ expression ~ ")" }
infix   = _{ add | subtract | multiply | divide | modulo | power | concatenate }

int    = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
float  = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
string =  { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
cast   =  { "(int)" | "(float)" | "(string)" }

add         = { "+" }
subtract    = { "-" }
multiply    = { "*" }
divide      = { "/" }
modulo      = { "%" }
power       = { "^" }
concatenate = { ".." }

conditional_operator = { ">" | "<" | ">=" | "<=" | "==" | "!=" }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC)* }
dataType   =  { "int" | "float" | "string" }

WHITESPACE = _{ " " | "\t" }
